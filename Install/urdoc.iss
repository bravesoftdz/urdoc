; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define AppBinDir "urdoc"
#define AppBin SourcePath+"\"+AppBinDir
#define AppVersion GetFileVersion(AppBin+"\urdoc.exe")
#define AppPublisher "NextSoft"
#define AppPublisherURL "http://nextsoft.ru"
#define OutputDir SourcePath+"\_output"
#define SetupIconFile ""
#define HistoryFile AppBin+"\history.rtf"
#define ReadmeFile AppBin+"\readme.rtf"
#define LicenseFile AppBin+"\license.rtf"

;#define DEMO

#ifdef DEMO
  #define AppName "Нотариус+ Демо"
  #define OutputBaseFilename "urdoc_demo_"+AppVersion
  #define AppID "{{9BF77A23-0325-49F7-B87A-E89278DBE046}"
  #define SecurityFile AppBin+"\urdoc_demo.db"
  #define DatabaseFile AppBin+"\urdoc_demo.fdb"
#else
  #define AppName "Нотариус+"
  #define OutputBaseFilename "urdoc_"+AppVersion
  #define AppID "{{6093C10B-7130-40A4-B805-D30B63674C7E}"
  #define SecurityFile AppBin+"\urdoc.db"
  #define DatabaseFile AppBin+"\urdoc.fdb"
#endif

[Setup]
AppName={#AppName}
AppVerName={#AppName} {#AppVersion}
AppPublisher={#AppPublisher}
AppPublisherURL={#AppPublisherURL}
AppSupportURL={#AppPublisherURL}
AppUpdatesURL={#AppPublisherURL}
DefaultDirName={pf}\{#AppPublisher}\{#AppName}
DefaultGroupName={#AppName}
OutputDir={#OutputDir}
OutputBaseFilename={#OutputBaseFilename}
InfoAfterFile={#HistoryFile}
InfoBeforeFile={#ReadmeFile}
Compression=lzma/ultra
SolidCompression=true
AppID={#AppID}
InternalCompressLevel=ultra
VersionInfoVersion={#AppVersion}
VersionInfoCompany={#AppPublisher}
VersionInfoDescription={#AppName} Setup
VersionInfoTextVersion={#AppVersion}
VersionInfoCopyright={#AppPublisher}
MinVersion=4.0.950,5.0.2195
AppCopyright={#AppPublisher}
AppMutex=nextsoft_urdoc
LicenseFile={#LicenseFile}
EnableDirDoesntExistWarning=true
AppVersion={#AppVersion}
UninstallDisplayName={#AppName} {#AppVersion}
SetupIconFile={#SetupIconFile}
UserInfoPage=true
PrivilegesRequired=admin

[Languages]
Name: russian; MessagesFile: compiler:Languages\Russian.isl

[Types]
Name: full; Description: Полная установка; Flags: iscustom

[Files]
Source: {#AppBin}\dbrtl100.bpl; DestDir: {app}; Flags: replacesameversion; Components: client
Source: {#AppBin}\dsnap100.bpl; DestDir: {app}; Flags: replacesameversion; Components: client
Source: {#AppBin}\inet100.bpl; DestDir: {app}; Flags: replacesameversion; Components: client
Source: {#AppBin}\rtl100.bpl; DestDir: {app}; Flags: replacesameversion; Components: client
Source: {#AppBin}\vcl100.bpl; DestDir: {app}; Flags: replacesameversion; Components: client
Source: {#AppBin}\vcldb100.bpl; DestDir: {app}; Flags: replacesameversion; Components: client
Source: {#SecurityFile}; DestName: urdoc.db; DestDir: {app}; Flags: ignoreversion; Components: client
Source: {#AppBin}\gds32.dll; DestDir: {sys}; Flags: replacesameversion; Components: client
Source: {#AppBin}\midas.dll; DestDir: {app}; Flags: replacesameversion; Components: client
Source: {#AppBin}\seocrpt.dll; DestDir: {app}; Flags: replacesameversion; Components: client
Source: {#AppBin}\help.doc; DestDir: {app}; Flags: ignoreversion; Components: client
Source: {#AppBin}\Firebird-1.5.5.4926-3-Win32.exe; DestDir: {tmp}; Flags: deleteafterinstall ignoreversion; Components: server
Source: {#AppBin}\quickrestore.exe; DestDir: {app}; Flags: replacesameversion; Components: client
Source: {#AppBin}\urdoc.exe; DestDir: {app}; Flags: replacesameversion; Components: client
Source: {#DatabaseFile}; DestName: urdoc.fdb; DestDir: {app}; Flags: confirmoverwrite; Components: db
Source: {#AppBin}\urdoc.ini; DestDir: {app}; Flags: onlyifdoesntexist; Components: client
Source: {#AppBin}\history.rtf; DestDir: {app}; Flags: ignoreversion; Components: client
Source: {#AppBin}\license.rtf; DestDir: {app}; Flags: ignoreversion; Components: client
Source: {#AppBin}\readme.rtf; DestDir: {app}; Flags: ignoreversion; Components: client
Source: {#AppBin}\vcl100.rus; DestDir: {app}; Flags: replacesameversion; Components: client
Source: {#AppBin}\vcldb100.rus; DestDir: {app}; Flags: replacesameversion; Components: client

[Icons]
Name: {userprograms}\{#AppPublisher}\{#AppName}\Запустить; Filename: {app}\urdoc.exe; Parameters: /config urdoc.ini; WorkingDir: {app}; IconFilename: {app}\urdoc.exe; Comment: Запустить {#AppName} {#AppVersion}; Components: client
Name: {userprograms}\{#AppPublisher}\{#AppName}\Восстановление; Filename: {app}\quickrestore.exe; WorkingDir: {app}; IconFilename: {app}\quickrestore.exe; Comment: Запустить быстрое восстановление; Components: client
Name: {userprograms}\{#AppPublisher}\{#AppName}\Руководство пользователя; Filename: {app}\help.doc; WorkingDir: {app}; IconFilename: {app}\help.doc; Comment: Руководство пользователя {#AppName} {#AppVersion}; Components: client
Name: {userprograms}\{#AppPublisher}\{#AppName}\Лицензия; Filename: {app}\license.rtf; WorkingDir: {app}; IconFilename: {app}\license.rtf; Comment: Лицензия {#AppName} {#AppVersion}; Components: client
Name: {userprograms}\{#AppPublisher}\{#AppName}\История; Filename: {app}\history.rtf; WorkingDir: {app}; IconFilename: {app}\history.rtf; Comment: История {#AppName} {#AppVersion}; Components: client
Name: {userprograms}\{#AppPublisher}\{#AppName}\Удалить; Filename: {uninstallexe}; WorkingDir: {app}; IconFilename: {uninstallexe}; Comment: Удалить {#AppName} {#AppVersion}; Components: client
Name: {userdesktop}\{#AppName}; Filename: {app}\urdoc.exe; Parameters: /config urdoc.ini; WorkingDir: {app}; IconFilename: {app}\urdoc.exe; Comment: {#AppName} {#AppVersion}; Components: client

[Components]
Name: client; Description: Программа {#AppName} {#AppVersion}; Types: full
Name: server; Description: Сервер баз данных Firebird 1.5.5.4926; Types: full
Name: db; Description: База данных {#AppName}; Types: full

[Run]
Filename: {tmp}\Firebird-1.5.5.4926-3-Win32.exe; WorkingDir: {tmp}; Components: server; StatusMsg: Установка сервера Firebird; Parameters: /silent; Flags: runhidden
Filename: {sys}\regsvr32.exe; Parameters: """{app}\midas.dll"" /s"; WorkingDir: {sys}; Flags: runhidden waituntilidle; Components: client
Filename: {app}\urdoc.exe; Parameters: /config urdoc.ini; WorkingDir: {app}; Flags: nowait postinstall skipifsilent; Components: client; Description: Запустить {#AppName}

[Code]
var
  PageKey: TWizardPage;
  EditKey: TEdit;
  PageUpdate: TWizardPage;
  EditUser: TEdit;
  EditPass: TPasswordEdit;

function PageKeyCreatePage(PreviousPageId: Integer): Integer;
var
  LabelInfo: TLabel;
  LabelKey: TLabel;
begin
  PageKey := CreateCustomPage(PreviousPageId,
                              'Регистрация секретного ключа',
                              'Пожалуйста, введите секретный ключ для работы с базой данных.');

  LabelInfo := TLabel.Create(PageKey);
  with LabelInfo do
  begin
    Parent := PageKey.Surface;
    Caption :='   Для регистрации программы {#AppName}, необходимо указать секретный ключ.';
    Left := ScaleX(0);
    Top := ScaleY(8);
    Width := ScaleX(400);
    Height := ScaleY(55);
    AutoSize := False;
    WordWrap := True;
  end;

  LabelKey := TLabel.Create(PageKey);
  with LabelKey do
  begin
    Parent := PageKey.Surface;
    Caption :='Секретный ключ:';
    Left := ScaleX(23);
    Top := ScaleY(90);
    Width := ScaleX(94);
    Height := ScaleY(13);
    Alignment := taRightJustify;
    FocusControl := EditUser;
  end;

  EditKey := TEdit.Create(PageKey);
  with EditKey do
  begin
    Parent := PageKey.Surface;
    Left := ScaleX(125);
    Top := ScaleY(86);
    Width := ScaleX(250);
    Height := ScaleY(21);
    MaxLength:=32;
    TabOrder := 1;
  end;

  Result := PageKey.ID;
end;

function PageUpdateCreatePage(PreviousPageId: Integer): Integer;
var
  LabelInfo: TLabel;
  LabelUser: TLabel;
  LabelPass: TLabel;
begin
  PageUpdate := CreateCustomPage(PreviousPageId,
                                 'Установка параметров соединения',
                                 'Пожалуйста, введите параметры соединения с базой данных.');

  LabelInfo := TLabel.Create(PageUpdate);
  with LabelInfo do
  begin
    Parent := PageUpdate.Surface;
    Caption :='   Для корректной работы программы {#AppName}, необходимо указать пользователя сервера'+
              ' баз данных Firebird, имеющего права Администратора. '+
              'По умолчанию пользователь = SYSDBA, пароль = masterkey';
    Left := ScaleX(0);
    Top := ScaleY(8);
    Width := ScaleX(400);
    Height := ScaleY(55);
    AutoSize := False;
    WordWrap := True;
  end;

  LabelUser := TLabel.Create(PageUpdate);
  with LabelUser do
  begin
    Parent := PageUpdate.Surface;
    Caption :='Пользователь:';
    Left := ScaleX(23);
    Top := ScaleY(90);
    Width := ScaleX(94);
    Height := ScaleY(13);
    Alignment := taRightJustify;
    FocusControl := EditUser;
  end;

  EditUser := TEdit.Create(PageUpdate);
  with EditUser do
  begin
    Parent := PageUpdate.Surface;
    Left := ScaleX(125);
    Top := ScaleY(86);
    Width := ScaleX(100);
    Height := ScaleY(21);
    Text:='SYSDBA';
    TabOrder := 1;
  end;

  LabelPass := TLabel.Create(PageUpdate);
  with LabelPass do
  begin
    Parent := PageUpdate.Surface;
    Caption := 'Пароль:';
    Left := ScaleX(244);
    Top := ScaleY(90);
    Width := ScaleX(41);
    Height := ScaleY(13);
    Alignment := taRightJustify;
    FocusControl := EditPass;
  end;

  EditPass := TPasswordEdit.Create(PageUpdate);
  with EditPass do
  begin
    Parent := PageUpdate.Surface;
    Left := ScaleX(294);
    Top := ScaleY(86);
    Width := ScaleX(100);
    Height := ScaleY(21);
    TabOrder := 2;
    Text := 'masterkey';
  end;

  Result := PageUpdate.ID;
end;

procedure InitializeWizard();
var
  PageId: Integer;
begin
  PageId:=PageKeyCreatePage(wpSelectComponents);
  PageUpdateCreatePage(PageId);
end;

function ShouldSkipPage(PageID: Integer): Boolean;
begin
  Result:=false;
  if PageID=PageKey.ID then begin
    #ifdef DEMO
      Result:=true;
    #else
      Result:=not WizardForm.ComponentsList.Checked[0];
    #endif
  end;
  if PageID=PageUpdate.ID then begin
    Result:=WizardForm.ComponentsList.Checked[1];
  end;
end;

function CheckKey: Boolean;
var
  S: String;
  i: Integer;
begin
  Result:=false;
  S:=UpperCase(EditKey.Text);
  if (Length(S)<>32) then begin
    exit;
  end else begin
    for i:=1 to Length(S) do begin
      if not ((S[i]<>'0') or (S[i]<>'1') or (S[i]<>'2') or (S[i]<>'3') or (S[i]<>'4') or
             (S[i]<>'5') or (S[i]<>'6') or (S[i]<>'7') or (S[i]<>'8') or (S[i]<>'9') or
             (S[i]<>'A') or (S[i]<>'B') or (S[i]<>'C') or (S[i]<>'D') or (S[i]<>'E') or
             (S[i]<>'F')) then begin
        exit;
      end;
    end;
    Result:=true;
  end;
end;

function NextButtonClick(CurPageID: Integer): Boolean;
begin
  Result:=true;
  if CurPageID=PageKey.ID then begin
    Result:=CheckKey;
    if not Result then begin
      MsgBox('Неверный ключ.',mbError,MB_OK);
    end;
  end;
end;

procedure CurStepChanged(CurStep: TSetupStep);
var
  IniFile: String;
  DatabaseName: String;
  FileHelp: String;
  ResultCode: Integer;
  S: String;
begin
  case CurStep of
    ssPostInstall: begin

      if WizardForm.ComponentsList.Checked[0] then begin

        IniFile:=ExpandConstant('{app}\urdoc.ini');
        if FileExists(IniFile) then begin
          if WizardForm.ComponentsList.Checked[2] then begin
            DatabaseName:=ExpandConstant('{app}\urdoc.fdb');
            SetIniString('Main','DataBaseName',DatabaseName,IniFile);
            SetIniString('Databases','Текущая база',DatabaseName,IniFile);
          end;

          FileHelp:=ExpandConstant('{app}\help.doc');
          SetIniString('Main','FileHelp',FileHelp,IniFile);
        end;

        #ifdef DEMO
        #else
          S:='/key "'+EditKey.Text+'"';
          Exec(ExpandConstant('{app}\urdoc.exe'),S,ExpandConstant('{app}'),SW_SHOWNORMAL,ewWaitUntilTerminated,ResultCode);
        #endif

        S:='/user "'+EditUser.Text+'" "'+EditPass.Text+'"';
        Exec(ExpandConstant('{app}\urdoc.exe'),S,ExpandConstant('{app}'),SW_SHOWNORMAL,ewWaitUntilTerminated,ResultCode);

      end;
      
      BringToFrontAndRestore();
    end;
  end;
end;
